" Vim indent file
" Language:	R
" Author:	Jakson Alves de Aquino <jalvesaq@gmail.com>
" URL:		http://www.vim.org/scripts/script.php?script_id=2628
" Last Change:	Fri Jan 28, 2011  09:30AM


" Only load this indent file when no other was loaded.
if exists("b:did_r_indent")
  finish
endif
let b:did_r_indent = 1

setlocal indentkeys=0{,0},:,!^F,o,O,e
setlocal indentexpr=GetRIndent()

" Only define the function once.
if exists("*GetRIndent")
  finish
endif

" This function is also used by r-plugin/common_global.vim
function RDelete_quotes(line)
  let i = 0
  let j = 0
  let line1 = ""
  let llen = strlen(a:line)
  while i < llen
    if a:line[i] == '"'
      let i += 1
      while !(a:line[i] == '"' && ((i > 1 && a:line[i-1] == '\' && a:line[i-2] == '\') || a:line[i-1] != '\')) && i < llen
	let i += 1
      endwhile
      if a:line[i] == '"'
	let i += 1
      endif
    else
      if a:line[i] == "'"
	let i += 1
	while !(a:line[i] == "'" && ((i > 1 && a:line[i-1] == '\' && a:line[i-2] == '\') || a:line[i-1] != '\')) && i < llen
	  let i += 1
	endwhile
	if a:line[i] == "'"
	  let i += 1
	endif
      endif
    endif
    if i == llen
      break
    endif
    let line1 = line1 . a:line[i]
    let j += 1
    let i += 1
  endwhile
  return line1
endfunction

" Convert foo(bar()) int foo()
function s:RDelete_parens(line)
  if s:Get_paren_balance(a:line, "(", ")") != 0
    return a:line
  endif
  let i = 0
  let j = 0
  let line1 = ""
  let llen = strlen(a:line)
  while i < llen
    let line1 = line1 . a:line[i]
    if a:line[i] == '('
      let nop = 1
      while nop > 0
	let i += 1
	if a:line[i] == ')'
	  let nop -= 1
	else
	  if a:line[i] == '('
	    let nop += 1 
	  endif
	endif
      endwhile
      let line1 = line1 . a:line[i]
    endif
    let i += 1
  endwhile
  return line1
endfunction

function! s:Get_paren_balance(line, o, c)
  let line2 = substitute(a:line, a:o, "", "g")
  let openp = strlen(a:line) - strlen(line2)
  let line3 = substitute(line2, a:c, "", "g")
  let closep = strlen(line2) - strlen(line3)
  return openp - closep
endfunction

function! s:Get_bracket_balance(line, cline)
  let pb = s:Get_paren_balance(a:line, '(', ')')
  let pb += s:Get_paren_balance(a:line, '[', ']')
  let pb += s:Get_paren_balance(a:line, '{', '')
  let pb += s:Get_paren_balance(a:cline, '', '}')
  return pb
endfunction

function! s:Get_matching_brace(linenr, o, c)
  let line = getline(a:linenr)
  let pb = s:Get_paren_balance(line, a:o, a:c)
  let i = a:linenr
  while pb != 0 && i > 1
    let i -= 1
    let pb += s:Get_paren_balance(s:SanitizeRLine(getline(i)), a:o, a:c)
  endwhile
  return i
endfunction

" Get previous relevant line. Search back until getting a line that isn't
" comment or blank
function s:Get_prev_line( lineno )
   let lnum = a:lineno - 1
   let data = getline( lnum )
   while lnum > 0 && (data =~ '^\s*#' || data =~ '^\s*$')
      let lnum = lnum - 1
      let data = getline( lnum )
   endwhile
   return lnum
endfunction

" Count groups of () because the indetation should be different for
" '  if(T)' and '  if(T) something()'
function s:CountGroups(line)
  let ngroups = 0
  let i = 0
  let llen = strlen(a:line)
  while i < llen
    if a:line[i] == '('
      let ngroups += 1
      let k = 1
      let i += 1
      while i < llen && k > 0
        if a:line[i] == '('
          let k += 1
        endif
        if a:line[i] == ')'
          let k -= 1
        endif
        let i += 1
      endwhile
    endif
    let i += 1
  endwhile
  return ngroups
endfunction

" Delete from '#' to the end of the line, unless the '#' is inside a string.
function s:SanitizeRLine(line)
  let newline = RDelete_quotes(a:line)
  let newline = s:RDelete_parens(newline)
  let newline = substitute(newline, '#.*', "", "")
  return newline
endfunction

function GetRIndent()

  let clnum = line(".")    " current line
  let cline = s:SanitizeRLine(getline(clnum))

  if cline =~ ".*}$"
    let indline = s:Get_matching_brace(clnum, '{', '}')
    if indline > 0
      return indent(indline)
    endif
  endif

  " Find the first non blank line above the current line
  let lnum = s:Get_prev_line(clnum)
  " Hit the start of the file, use zero indent.
  if lnum == 0
    return 0
  endif

  let line = s:SanitizeRLine(getline(lnum))
  " Find the beginning of the block
  let post_block = 0
  if line =~ ".*}$"
    let post_block = 1
    let lnum = s:Get_matching_brace(lnum, '{', '}')
    let line = s:SanitizeRLine(getline(lnum))
    if line =~ '^\s*{\s*$' && lnum > 0
      lnum = s:Get_prev_line(lnum)
      line = s:SanitizeRLine(getline(lnum))
    endif
  endif

  if line =~ '.*{\s*$' && post_block == 0
    let ind = indent(lnum) + &sw
    return ind
  endif

  let pb = s:Get_paren_balance(line, '(', ')')
  if pb > 0
    let ind = indent(lnum) + pb * &sw
    if exists("g:r_indent_align_args") && g:r_indent_align_args == 1
      let ind = stridx(line, "(") + 1
    endif
    return ind
  endif

  let post_fun = 0
  if line =~ '.*)\s*$' && pb < 0
    let post_fun = 1
    let lnum = s:Get_matching_brace(lnum, '(', ')')
    let line = s:SanitizeRLine(getline(lnum))
  endif

  let plnum = s:Get_prev_line(lnum)
  if plnum > 0
    let pline = s:SanitizeRLine(getline(plnum))
    let ppost_block = 0
    if pline =~ '.*}$'
      let ppost_block = 1
      let plnum = s:Get_matching_brace(plnum, '{', '}')
      let pline = s:SanitizeRLine(getline(plnum))
      if pline =~ '^\s*{\s*$' && plnum > 0
	plnum = s:Get_prev_line(plnum)
	pline = s:SanitizeRLine(getline(plnum))
      endif
    endif
    let pplnum = s:Get_prev_line(plnum)
    if pplnum > 0
      let ppline = s:SanitizeRLine(getline(pplnum))
    endif
  else
    pplnum = 0
  endif

  while pplnum > 0 && ((ppline =~ '\(if\|while\|for\)\s*(.*)\s*$' && s:CountGroups(ppline) == 1)) && pline !~ '.*{\s*$'
    let plnum = pplnum
    let pline = ppline
    let pplnum = s:Get_prev_line(pplnum)
    let ppline = s:SanitizeRLine(getline(pplnum))
  endwhile

  let ind = indent(lnum)

  if post_block == 0 && post_fun == 0
    let pb = s:Get_bracket_balance(line, cline)
    let ind += pb * &sw
  endif

  " 'if', 'for', 'while' or 'else' without '{'
  if (line =~ '\(if\|while\|for\)\s*(.*)\s*$' && s:CountGroups(line) == 1) || line =~ '^\s*else\s*$'
    let ind = ind + &sw
    if cline =~ '^\s*{'
      let ind = ind - &sw
    endif
    return ind
  endif

  if plnum > 0 && ((pline =~ '\(if\|while\|for\)\s*(.*)\s*$' && s:CountGroups(pline) == 1) || pline =~ '^\s*else\s*$')
    let ind = indent(plnum)
    if post_block == 0 post_fun == 0
      let pb = s:Get_bracket_balance(line, cline)
      let ind += pb * &sw
    endif
  endif


  return ind
endfunction

